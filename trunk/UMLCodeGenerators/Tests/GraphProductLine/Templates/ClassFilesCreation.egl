[%
////////////////////////////////////////////////////////////
//
// Author: Patricia Abascal Fernández
// Version: 1.3 
// Last modification: 20/03/2013 
// Description: Generates the files of all code classes. And
//				return a set of all the paths.
////////////////////////////////////////////////////////////
import "BidirectionalRelations.egl";
operation classFilesCreation(packages: Set, modelProject: Model, directory: String){
	var paths_empty: Set; 
	var paths_notempty: Set; 
	
	// Select bidirectional relations
	var bidirectionalRelations=bidirectionalRelations();  	
	for (pack in packages) {
	    var classes=pack.packagedElement.select(e|e.isTypeOf(Class));
	    // If the package is empty
	    if (classes.size()==0){
	    	var path="src\\" + pack.name + "\\";
	    	paths_empty.add(path);
	    // If it's not empty, create all the classes inside the package
	    }else{
			for (class in classes) { 
		  		var t := TemplateFactory.load('ClassCreation.egl');
				t.populate ('currentClass', class);	
				t.populate('currentPackage', pack);
				t.populate('currentModel', modelProject);  
				t.populate('bidirectionalRelations',bidirectionalRelations);  
				var path=directory+"\\src\\" + pack.name + "\\"+ class.name +'.cs';
				t.generate (path); 	
				paths_notempty.add(path);
		     }//for-class
		}//if
	}//for-pack
	// Set of two sets, one for empty and the other one for non empty paths
	var paths:List;
	paths.add(paths_empty);
	paths.add(paths_notempty);
	return paths;
}
%]